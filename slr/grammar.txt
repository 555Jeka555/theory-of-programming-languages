program -> PROGRAM IDENTIFIER SEMICOLON block
program -> PROGRAM IDENTIFIER SEMICOLON
block -> BEGIN stmt_list
block -> BEGIN END
stmt_list -> END DOT
stmt_list -> stmt stmt_list
stmt -> decl SEMICOLON
stmt -> assign SEMICOLON
stmt -> if_stmt
stmt -> while_stmt
stmt -> io SEMICOLON
stmt -> block
decl -> VAR ident_list COLON type
decl -> TYPE IDENTIFIER EQ type
type -> INT
type -> REAL
type -> ARRAY LEFT_BRACKET INTEGER RIGHT_BRACKET OF type
ident_list -> IDENTIFIER
ident_list -> IDENTIFIER COMMA ident_list
assign -> IDENTIFIER ASSIGN expr
assign -> IDENTIFIER LEFT_BRACKET expr RIGHT_BRACKET ASSIGN expr
if_stmt -> IF cond THEN stmt ELSE stmt
if_stmt -> IF cond THEN stmt
while_stmt -> WHILE cond LOOP stmt
io -> PRINT expr
io -> PRINT STRING
expr -> expr PLUS term
expr -> expr MINUS term
expr -> term
term -> term MULTIPLICATION factor
term -> term DIVIDE factor
term -> term DIV factor
term -> term MOD factor
term -> factor
factor -> MINUS factor
factor -> NOT factor
factor -> LEFT_PAREN expr RIGHT_PAREN
factor -> IDENTIFIER
factor -> IDENTIFIER LEFT_BRACKET expr RIGHT_BRACKET
factor -> INTEGER
factor -> FLOAT
factor -> TRUE
factor -> FALSE
cond -> expr rel_op expr
rel_op -> EQ
rel_op -> NOT_EQ
rel_op -> LESS
rel_op -> GREATER
rel_op -> LESS_EQ
rel_op -> GREATER_EQ